{ ... }:
{
  text.readme.parts.tmux =
    # markdown
    ''
      ## Tmux 

      [tmux](https://github.com/tmux/tmux/wiki) with configurations

    '';

  perSystem =
    { pkgs, ... }:
    let
      plugins = import ./_plugins.nix { inherit pkgs; };

      pluginLines = pkgs.lib.concatMapStringsSep "\n" (p: ''
        # ${p.package.pname or "unknown"}
        # ---------------------
        ${p.extraConfig or ""}
        run-shell ${p.package.rtp}
      '') (pkgs.lib.attrValues plugins);

      pluginConfig = pkgs.lib.concatStringsSep "\n" (pkgs.lib.flatten pluginLines);

      tmuxConf = ''
        # This file is generated by https://github.com/dsnn/flakes/tmux

        #========================#
        #          BASE          #
        #========================#

        ${builtins.readFile ./tmux.conf}

        #========================#
        #         PLUGINS        #
        #========================#

        ${pluginConfig}
      '';

      tmuxConfFile = pkgs.writeText "tmux.conf" tmuxConf;
    in
    {
      packages.tmux = pkgs.writeShellApplication {
        name = "tmux-wrapper";
        runtimeInputs = [ pkgs.tmux ];
        text = "tmux -f ${tmuxConfFile} \"$@\"";
      };
    };
}
